<h1 id="qualified-names-and-member-access">Qualified names and member access</h1>
<!--
Part of the Carbon Language project, under the Apache License v2.0 with LLVM
Exceptions. See /LICENSE for license information.
SPDX-License-Identifier: Apache-2.0 WITH LLVM-exception
-->

<!-- toc -->

<h2 id="table-of-contents">Table of contents</h2>
<ul>
<li><a href="#overview">Overview</a></li>
<li><a href="#member-resolution">Member resolution</a>
<ul>
<li><a href="#package-and-namespace-members">Package and namespace members</a></li>
<li><a href="#lookup-within-values">Lookup within values</a>
<ul>
<li><a href="#templates-and-generics">Templates and generics</a></li>
<li><a href="#lookup-ambiguity">Lookup ambiguity</a></li>
</ul></li>
</ul></li>
<li><a href="#impl-lookup"><code>impl</code> lookup</a></li>
<li><a href="#instance-binding">Instance binding</a></li>
<li><a href="#non-instance-members">Non-instance members</a></li>
<li><a href="#non-vacuous-member-access-restriction">Non-vacuous member access restriction</a></li>
<li><a href="#precedence-and-associativity">Precedence and associativity</a></li>
<li><a href="#alternatives-considered">Alternatives considered</a></li>
<li><a href="#references">References</a></li>
</ul>
<!-- tocstop -->

<h2 id="overview">Overview</h2>
<p>A <em>qualified name</em> is a <a href="../lexical_conventions/words.html">word</a> that is preceded by a period. The name is found within a contextually determined entity:</p>
<ul>
<li>In a member access expression, this is the entity preceding the period.</li>
<li>For a designator in a struct literal, the name is introduced as a member of the struct type.</li>
</ul>
<p>A <em>member access expression</em> allows a member of a value, type, interface, namespace, and so on to be accessed by specifying a qualified name for the member.</p>
<p>A member access expression is either a <em>simple</em> member access expression of the form:</p>
<ul>
<li><em>member-access-expression</em> ::= <em>expression</em> <code>.</code> <em>word</em></li>
</ul>
<p>or a <em>compound</em> member access of the form:</p>
<ul>
<li><em>member-access-expression</em> ::= <em>expression</em> <code>.</code> <code>(</code> <em>expression</em> <code>)</code></li>
</ul>
<p>Compound member accesses allow specifying a qualified member name.</p>
<p>For example:</p>
<pre class="carbon"><code>package Widgets api;
interface Widget {
  fn Grow[addr me: Self*](factor: f64);
}
class Cog {
  var size: i32;
  fn Make(size: i32) -&gt; Self;
  impl as Widgets.Widget;
}

fn GrowSomeCogs() {
  var cog1: Cog = Cog.Make(1);
  var cog2: Cog = cog1.Make(2);
  let cog1_size: i32 = cog1.size;
  cog1.Grow(1.5);
  cog2.(Cog.Grow)(cog1_size as f64);
  cog1.(Widget.Grow)(1.1);
  cog2.(Widgets.Cog.(Widgets.Widget.Grow))(1.9);
}</code></pre>
<p>A member access expression is processed using the following steps:</p>
<ul>
<li>First, the word or parenthesized expression to the right of the <code>.</code> is <a href="#member-resolution">resolved</a> to a specific member entity, called <code>M</code> in this document.</li>
<li>Then, if necessary, <a href="#impl-lookup"><code>impl</code> lookup</a> is performed to map from a member of an interface to a member of the relevant <code>impl</code>, potentially updating <code>M</code>.</li>
<li>Then, if necessary, <a href="#instance-binding">instance binding</a> is performed to locate the member subobject corresponding to a field name or to build a bound method object, producing the result of the member access expression.</li>
<li>If <a href="#non-instance-members">instance binding is not performed</a>, the result is <code>M</code>.</li>
</ul>
<h2 id="member-resolution">Member resolution</h2>
<p>The process of <em>member resolution</em> determines which member <code>M</code> a member access expression is referring to.</p>
<h3 id="package-and-namespace-members">Package and namespace members</h3>
<p>If the first operand is a package or namespace name, the expression must be a simple member access expression. The <em>word</em> must name a member of that package or namespace, and the result is the package or namespace member with that name.</p>
<p>An expression that names a package or namespace can only be used as the first operand of a member access or as the target of an <code>alias</code> declaration.</p>
<pre><code>namespace MyNamespace;
fn MyNamespace.MyFunction() {}

// ‚úÖ OK, can alias a namespace.
alias MyNS = MyNamespace;
fn CallMyFunction() { MyNS.MyFunction(); }

// ‚ùå Error: a namespace is not a value.
let MyNS2:! auto = MyNamespace;

fn CallMyFunction2() {
  // ‚ùå Error: cannot perform compound member access into a namespace.
  MyNamespace.(MyNamespace.MyFunction)();
}</code></pre>
<h3 id="lookup-within-values">Lookup within values</h3>
<p>When the first operand is not a package or namespace name, there are three remaining cases we wish to support:</p>
<ul>
<li>The first operand is a value, and lookup should consider members of the value's type.</li>
<li>The first operand is a type, and lookup should consider members of that type. For example, <code>i32.Least</code> should find the member constant <code>Least</code> of the type <code>i32</code>.</li>
<li>The first operand is a type-of-type, and lookup should consider members of that type-of-type. For example, <code>Addable.Add</code> should find the member function <code>Add</code> of the interface <code>Addable</code>. Because a type-of-type is a type, this is a special case of the previous bullet.</li>
</ul>
<p>Note that because a type is a value, and a type-of-type is a type, these cases are overlapping and not entirely separable.</p>
<p>If any of the above lookups ever looks for members of a type parameter, it should consider members of the type-of-type, treating the type parameter as an archetype.</p>
<p><strong>Note:</strong> If lookup is performed into a type that involves a template parameter, the lookup will be performed both in the context of the template definition and in the context of the template instantiation, as described in <a href="#templates-and-generics">templates and generics</a>.</p>
<p>For a simple member access, the word is looked up in the following types:</p>
<ul>
<li>If the first operand can be evaluated and evaluates to a type, that type.</li>
<li>If the type of the first operand can be evaluated, that type.</li>
<li>If the type of the first operand is a generic type parameter, and the type of that generic type parameter can be evaluated, that type-of-type.</li>
</ul>
<p>The results of these lookups are <a href="#lookup-ambiguity">combined</a>.</p>
<p>For a compound member access, the second operand is evaluated as a constant to determine the member being accessed. The evaluation is required to succeed and to result in a member of a type or interface.</p>
<p>For example:</p>
<pre><code>interface Printable {
  fn Print[me: Self]();
}
external impl i32 as Printable;
class Point {
  var x: i32;
  var y: i32;
  // Internal impl injects the name `Print` into class `Point`.
  impl as Printable;
}

fn PrintPointTwice() {
  var p: Point = {.x = 0, .y = 0};

  // ‚úÖ OK, `x` found in type of `p`, namely `Point`.
  p.x = 1;
  // ‚úÖ OK, `y` found in the type `Point`.
  p.(Point.y) = 1;

  // ‚úÖ OK, `Print` found in type of `p`, namely `Point`.
  p.Print();
  // ‚úÖ OK, `Print` found in the type `Printable`.
  p.(Printable.Print)();
}
fn GenericPrint[T:! Printable](a: T) {
  // ‚úÖ OK, type of `a` is the type parameter `T`;
  // `Print` found in the type of `T`, namely `Printable`.
  a.Print();
}
fn CallGenericPrint(p: Point) {
  GenericPrint(p);
}</code></pre>
<h4 id="templates-and-generics">Templates and generics</h4>
<p>If the value or type of the first operand depends on a template or generic parameter, the lookup is performed from a context where the value of that parameter is unknown. Evaluation of an expression involving the parameter may still succeed, but will result in a symbolic value involving that parameter.</p>
<pre><code>class GenericWrapper(T:! Type) {
  var field: T;
}
fn F[T:! Type](x: GenericWrapper(T)) -&gt; T {
  // ‚úÖ OK, finds `GenericWrapper(T).field`.
  return x.field;
}

class TemplateWrapper(template T:! Type) {
  var field: T;
}
fn G[template T:! Type](x: TemplateWrapper(T)) -&gt; T {
  // ü§∑ Not yet decided.
  return x.field;
}</code></pre>
<blockquote>
<p><strong>TODO:</strong> The behavior of <code>G</code> above is not yet fully decided. If class templates can be specialized, then we cannot know the members of <code>TemplateWrapper(T)</code> without knowing <code>T</code>, so this first lookup will find nothing. In any case, as described below, the lookup will be performed again when <code>T</code> is known.</p>
</blockquote>
<p>If the value or type depends on any template parameters, the lookup is redone from a context where the values of those parameters are known, but where the values of any generic parameters are still unknown. The lookup results from these two contexts are <a href="#lookup-ambiguity">combined</a>.</p>
<p><strong>Note:</strong> All lookups are done from a context where the values of any generic parameters that are in scope are unknown. Unlike for a template parameter, the actual value of a generic parameter never affects the result of member resolution.</p>
<pre class="carbon"><code>class Cowboy { fn Draw[me: Self](); }
interface Renderable {
  fn Draw[me: Self]();
}
external impl Cowboy as Renderable { fn Draw[me: Self](); }
fn DrawDirect(c: Cowboy) { c.Draw(); }
fn DrawGeneric[T:! Renderable](c: T) { c.Draw(); }
fn DrawTemplate[template T:! Renderable](c: T) { c.Draw(); }

fn Draw(c: Cowboy) {
  // ‚úÖ Calls member of `Cowboy`.
  DrawDirect(c);
  // ‚úÖ Calls member of `impl Cowboy as Renderable`.
  DrawGeneric(c);
  // ‚ùå Error: ambiguous.
  DrawTemplate(c);
}

class RoundWidget {
  external impl as Renderable {
    fn Draw[me: Self]();
  }
  alias Draw = Renderable.Draw;
}

class SquareWidget {
  fn Draw[me: Self]() {}
  external impl as Renderable {
    alias Draw = Self.Draw;
  }
}

fn DrawWidget(r: RoundWidget, s: SquareWidget) {
  // ‚úÖ OK, lookup in type and lookup in type-of-type find the same entity.
  DrawTemplate(r);

  // ‚úÖ OK, lookup in type and lookup in type-of-type find the same entity.
  DrawTemplate(s);

  // ‚úÖ OK, found in type.
  r.Draw();
  s.Draw();
}</code></pre>
<h4 id="lookup-ambiguity">Lookup ambiguity</h4>
<p>Multiple lookups can be performed when resolving a member access expression. If more than one member is found, after performing <a href="#impl-lookup"><code>impl</code> lookup</a> if necessary, the lookup is ambiguous, and the program is invalid. Similarly, if no members are found, the program is invalid. Otherwise, the result of combining the lookup results is the unique member that was found.</p>
<h2 id="impl-lookup"><code>impl</code> lookup</h2>
<p>When the second operand of a member access expression resolves to a member of an interface <code>I</code>, and the first operand is a value other than a type-of-type, <em><code>impl</code> lookup</em> is performed to map the member of the interface to the corresponding member of the relevant <code>impl</code>. The member of the <code>impl</code> replaces the member of the interface in all further processing of the member access expression.</p>
<pre class="carbon"><code>interface Addable {
  // #1
  fn Add[me: Self](other: Self) -&gt; Self;
  // #2
  default fn Sum[Seq:! Iterable where .ValueType = Self](seq: Seq) -&gt; Self {
    // ...
  }
}

class Integer {
  impl as Addable {
    // #3
    fn Add[me: Self](other: Self) -&gt; Self;
    // #4, generated from default implementation for #2.
    // fn Sum[...](...);
  }
}

fn SumIntegers(v: Vector(Integer)) -&gt; Integer {
  // Member resolution resolves the name `Sum` to #2.
  // `impl` lookup then locates the `impl Integer as Addable`,
  // and determines that the member access refers to #4,
  // which is then called.
  return Integer.Sum(v);
}

fn AddTwoIntegers(a: Integer, b: Integer) -&gt; Integer {
  // Member resolution resolves the name `Add` to #1.
  // `impl` lookup then locates the `impl Integer as Addable`,
  // and determines that the member access refers to #3.
  // Finally, instance binding will be performed as described later.
  // This can be written more verbosely and explicitly as any of:
  // -   `return a.(Integer.Add)(b);`
  // -   `return a.(Addable.Add)(b);`
  // -   `return a.(Integer.(Addable.Add))(b);`
  return a.Add(b);
}</code></pre>
<p>The type <code>T</code> that is expected to implement <code>I</code> depends on the first operand of the member access expression, <code>V</code>:</p>
<ul>
<li>If <code>V</code> can be evaluated and evaluates to a type, then <code>T</code> is <code>V</code>.
<pre class="carbon"><code>// `V` is `Integer`. `T` is `V`, which is `Integer`.
// Alias refers to #2.
alias AddIntegers = Integer.Add;</code></pre></li>
<li>Otherwise, <code>T</code> is the type of <code>V</code>.
<pre class="carbon"><code>let a: Integer = {};
// `V` is `a`. `T` is the type of `V`, which is `Integer`.
// `a.Add` refers to #2.
let twice_a: Integer = a.Add(a);</code></pre></li>
</ul>
<p>The appropriate <code>impl T as I</code> implementation is located. The program is invalid if no such <code>impl</code> exists. When <code>T</code> or <code>I</code> depends on a generic parameter, a suitable constraint must be specified to ensure that such an <code>impl</code> will exist. When <code>T</code> or <code>I</code> depends on a template parameter, this check is deferred until the argument for the template parameter is known.</p>
<p><code>M</code> is replaced by the member of the <code>impl</code> that corresponds to <code>M</code>.</p>
<pre class="carbon"><code>interface I {
  // #1
  default fn F[me: Self]() {}
  let N:! i32;
}
class C {
  impl as I where .N = 5 {
    // #2
    fn F[me: C]() {}
  }
}

// `V` is `I` and `M` is `I.F`. Because `V` is a type-of-type,
// `impl` lookup is not performed, and the alias binds to #1.
alias A1 = I.F;

// `V` is `C` and `M` is `I.F`. Because `V` is a type, `impl`
// lookup is performed with `T` being `C`, and the alias binds to #2.
alias A2 = C.F;

let c: C = {};

// `V` is `c` and `M` is `I.N`. Because `V` is a non-type, `impl`
// lookup is performed with `T` being the type of `c`, namely `C`, and
// `M` becomes the associated constant from `impl C as I`.
// The value of `Z` is 5.
let Z: i32 = c.N;</code></pre>
<p><a href="#instance-binding">Instance binding</a> may also apply if the member is an instance member.</p>
<pre class="carbon"><code>var c: C;
// `V` is `c` and `M` is `I.F`. Because `V` is not a type, `T` is the
// type of `c`, which is `C`. `impl` lookup is performed, and `M` is
// replaced with #2. Then instance binding is performed.
c.F();</code></pre>
<p><strong>Note:</strong> When an interface member is added to a class by an alias, <code>impl</code> lookup is not performed as part of handling the alias, but will happen when naming the interface member as a member of the class.</p>
<pre class="carbon"><code>interface Renderable {
  // #1
  fn Draw[me: Self]();
}

class RoundWidget {
  external impl as Renderable {
    // #2
    fn Draw[me: Self]();
  }
  // `Draw` names the member of the `Renderable` interface.
  alias Draw = Renderable.Draw;
}

class SquareWidget {
  // #3
  fn Draw[me: Self]() {}
  external impl as Renderable {
    alias Draw = Self.Draw;
  }
}

fn DrawWidget(r: RoundWidget, s: SquareWidget) {
  // ‚úÖ OK: In the inner member access, the name `Draw` resolves to the
  // member `Draw` of `Renderable`, #1, which `impl` lookup replaces with
  // the member `Draw` of `impl RoundWidget as Renderable`, #2.
  // The outer member access then forms a bound member function that
  // calls #2 on `r`, as described in &quot;Instance binding&quot;.
  r.(RoundWidget.Draw)();

  // ‚úÖ OK: In the inner member access, the name `Draw` resolves to the
  // member `Draw` of `SquareWidget`, #3.
  // The outer member access then forms a bound member function that
  // calls #3 on `s`.
  s.(SquareWidget.Draw)();

  // ‚ùå Error: In the inner member access, the name `Draw` resolves to the
  // member `Draw` of `SquareWidget`, #3.
  // The outer member access fails because we can&#39;t call
  // #3, `Draw[me: SquareWidget]()`, on a `RoundWidget` object `r`.
  r.(SquareWidget.Draw)();

  // ‚ùå Error: In the inner member access, the name `Draw` resolves to the
  // member `Draw` of `Renderable`, #1, which `impl` lookup replaces with
  // the member `Draw` of `impl RoundWidget as Renderable`, #2.
  // The outer member access fails because we can&#39;t call
  // #2, `Draw[me: RoundWidget]()`, on a `SquareWidget` object `s`.
  s.(RoundWidget.Draw)();
}

base class WidgetBase {
  // ‚úÖ OK, even though `WidgetBase` does not implement `Renderable`.
  alias Draw = Renderable.Draw;
  fn DrawAll[T:! Renderable](v: Vector(T)) {
    for (var w: T in v) {
      // ‚úÖ OK. Unqualified lookup for `Draw` finds alias `WidgetBase.Draw`
      // to `Renderable.Draw`, which does not perform `impl` lookup yet.
      // Then the compound member access expression performs `impl` lookup
      // into `impl T as Renderable`, since `T` is known to implement
      // `Renderable`. Finally, the member function is bound to `w` as
      // described in &quot;Instance binding&quot;.
      w.(Draw)();
      // ‚ùå Error: `Self.Draw` performs `impl` lookup, which fails
      // because `WidgetBase` does not implement `Renderable`.
      w.(Self.Draw)();
    }
  }
}

class TriangleWidget extends WidgetBase {
  external impl as Renderable;
}
fn DrawTriangle(t: TriangleWidget) {
  // ‚úÖ OK: name `Draw` resolves to `Draw` member of `WidgetBase`, which
  // is `Renderable.Draw`. Then impl lookup replaces that with `Draw`
  // member of `impl TriangleWidget as Renderable`.
  t.Draw();
}</code></pre>
<h2 id="instance-binding">Instance binding</h2>
<p>If member resolution and <code>impl</code> lookup produce a member <code>M</code> that is an instance member -- that is, a field or a method -- and the first operand <code>V</code> of <code>.</code> is a value other than a type, then <em>instance binding</em> is performed, as follows:</p>
<ul>
<li><p>For a field member in class <code>C</code>, <code>V</code> is required to be of type <code>C</code> or of a type derived from <code>C</code>. The result is the corresponding subobject within <code>V</code>. The result is an lvalue if <code>V</code> is an lvalue.</p>
<pre class="carbon"><code>var dims: auto = {.width = 1, .height = 2};
// `dims.width` denotes the field `width` of the object `dims`.
Print(dims.width);
// `dims` is an lvalue, so `dims.height` is an lvalue.
dims.height = 3;</code></pre></li>
<li><p>For a method, the result is a <em>bound method</em>, which is a value <code>F</code> such that a function call <code>F(args)</code> behaves the same as a call to <code>M(args)</code> with the <code>me</code> parameter initialized by a corresponding recipient argument:</p>
<ul>
<li>If the method declares its <code>me</code> parameter with <code>addr</code>, the recipient argument is <code>&amp;V</code>.</li>
<li>Otherwise, the recipient argument is <code>V</code>.</li>
</ul>
<pre class="carbon"><code>class Blob {
  fn Mutate[addr me: Self*](n: i32);
}
fn F(p: Blob*) {
  // ‚úÖ OK, forms bound method `((*p).M)` and calls it.
  // This calls `Blob.Mutate` with `me` initialized by `&amp;(*p)`
  // and `n` initialized by `5`.
  (*p).Mutate(5);

  // ‚úÖ OK, same as above.
  let bound_m: auto = (*p).Mutate;
  bound_m(5);
}</code></pre></li>
</ul>
<h2 id="non-instance-members">Non-instance members</h2>
<p>If instance binding is not performed, the result is the member <code>M</code> determined by member resolution and <code>impl</code> lookup. Evaluating the member access expression evaluates <code>V</code> and discards the result.</p>
<p>An expression that names an instance member, but for which instance binding is not performed, can only be used as the second operand of a compound member access or as the target of an <code>alias</code> declaration.</p>
<pre class="carbon"><code>class C {
  fn StaticMethod();
  var field: i32;
  class Nested {}
}
fn CallStaticMethod(c: C) {
  // ‚úÖ OK, calls `C.StaticMethod`.
  C.StaticMethod();

  // ‚úÖ OK, evaluates expression `c` then calls `C.StaticMethod`.
  c.StaticMethod();

  // ‚ùå Error: name of instance member `C.field` can only be used in a
  // member access or alias.
  C.field = 1;
  // ‚úÖ OK, instance binding is performed by outer member access,
  // same as `c.field = 1;`
  c.(C.field) = 1;

  // ‚úÖ OK
  let T:! Type = C.Nested;
  // ‚ùå Error: value of `:!` binding is not constant because it
  // refers to local variable `c`.
  let U:! Type = c.Nested;
}</code></pre>
<h2 id="non-vacuous-member-access-restriction">Non-vacuous member access restriction</h2>
<p>The first operand of a member access expression must be used in some way: a compound member access must result in <code>impl</code> lookup, instance binding, or both. In a simple member access, this always holds, because the first operand is always used for lookup.</p>
<pre><code>interface Printable {
  fn Print[me: Self]();
}
external impl i32 as Printable {
  fn Print[me: Self]();
}
fn MemberAccess(n: i32) {
  // ‚úÖ OK: `Printable.Print` is the interface member.
  // `i32.(Printable.Print)` is the corresponding member of the `impl`.
  // `n.(i32.(Printable.Print))` is a bound member function naming that member.
  n.(i32.(Printable.Print))();

  // ‚úÖ Same as above, `n.(Printable.Print)` is effectively interpreted as
  // `n.(T.(Printable.Print))()`, where `T` is the type of `n`,
  // because `n` does not evaluate to a type. Performs impl lookup
  // and then instance binding.
  n.(Printable.Print)();
}

// ‚úÖ OK, member `Print` of interface `Printable`.
alias X1 = Printable.Print;
// ‚ùå Error, compound access doesn&#39;t perform impl lookup or instance binding.
alias X2 = Printable.(Printable.Print);
// ‚úÖ OK, member `Print` of `impl i32 as Printable`.
alias X3 = i32.(Printable.Print);
// ‚ùå Error, compound access doesn&#39;t perform impl lookup or instance binding.
alias X4 = i32.(i32.(Printable.Print));</code></pre>
<h2 id="precedence-and-associativity">Precedence and associativity</h2>
<p>Member access expressions associate left-to-right:</p>
<pre><code>class A {
  class B {
    fn F();
  }
}
interface B {
  fn F();
}
external impl A as B;

fn Use(a: A) {
  // Calls member `F` of class `A.B`.
  (a.B).F();
  // Calls member `F` of interface `B`, as implemented by type `A`.
  a.(B.F)();
  // Same as `(a.B).F()`.
  a.B.F();
}</code></pre>
<p>Member access has lower precedence than primary expressions, and higher precedence than all other expression forms.</p>
<pre><code>// ‚úÖ OK, `*` has lower precedence than `.`. Same as `(A.B)*`.
var p: A.B*;
// ‚úÖ OK, `1 + (X.Y)` not `(1 + X).Y`.
var n: i32 = 1 + X.Y;</code></pre>
<h2 id="alternatives-considered">Alternatives considered</h2>
<ul>
<li><a href="https://github.com/carbon-language/carbon-lang/tree/trunk/proposals/p0989.md#separate-syntax-for-static-versus-dynamic-access">Separate syntax for static versus dynamic access, such as <code>::</code> versus <code>.</code></a></li>
<li><a href="https://github.com/carbon-language/carbon-lang/tree/trunk/proposals/p0989.md#use-a-different-lookup-rule-in-templates">Use a different lookup rule for names in templates</a></li>
<li><a href="https://github.com/carbon-language/carbon-lang/tree/trunk/proposals/p0989.md#meaning-of-typeinterface">Meaning of <code>Type.Interface</code></a></li>
</ul>
<h2 id="references">References</h2>
<ul>
<li>Proposal <a href="https://github.com/carbon-language/carbon-lang/pull/989">#989: member access expressions</a></li>
<li><a href="https://github.com/carbon-language/carbon-lang/issues/949">Question for leads: constrained template name lookup</a></li>
</ul>
